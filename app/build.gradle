buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


android {
    signingConfigs {
        release {
            keyAlias ""
            keyPassword ""
            storeFile file('../extras/release.keystore')
            storePassword ""
        }
    }
    compileSdkVersion 24
    buildToolsVersion '24.0.0'

    defaultConfig {
        applicationId "com.kbeanie.multipicker.sample"
        minSdkVersion 10
        targetSdkVersion 24
        versionCode 11
        versionName "1.1.2"
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dexOptions {
        incremental true
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(':app:assembleRelease')) {
        /**
         * Configure Release key
         */
        def props = new Properties()
        props.load(new FileInputStream(rootProject.file("extras/secret.properties")))
        android.signingConfigs.release.keyAlias props.alias
        android.signingConfigs.release.storePassword props.getProperty('keyStorePassword')
        android.signingConfigs.release.keyPassword props.getProperty('keyAliasPassword')
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile project(':multipicker')
    compile('com.crashlytics.sdk.android:crashlytics:2.6.1@aar') {
        transitive = true;
    }
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:recyclerview-v7:24.2.1'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'net.yslibrary.keyboardvisibilityevent:keyboardvisibilityevent:1.0.1'
}